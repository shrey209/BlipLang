Program         → StatementList

StatementList   → Statement StatementList
                | ε

Statement       → Declaration
                | Assignment
                | PrintStmt
                | IfStmt

Declaration     → 'let' Identifier '=' Expression ';'

Assignment      → Identifier '=' Expression ';'

PrintStmt       → 'print' Expression ';'

IfStmt          → 'if' '(' Expression ')' Block [ 'else' Block ]

Block           → '{' StatementList '}'

Expression      → Term ExpressionPrime

ExpressionPrime → '+' Term ExpressionPrime
                | '-' Term ExpressionPrime
                | ε

Term            → Factor TermPrime

TermPrime       → '*' Factor TermPrime
                | '/' Factor TermPrime
                | ε

Factor          → '(' Expression ')'
                | Number
                | Identifier

Number          → [0-9]+

Identifier      → [a-zA-Z_][a-zA-Z0-9_]*
